/**
Technical Report available at: https://abz2020.uni-ulm.de/resources/files/casestudyABZ2020v1.11.pdf

----- Cruise Control System ------

@author Lucas Pereira 68547
@author Ricardo Pereira 73577

**/

open util/ordering[Speed] as S
open util/ordering[SCSLever] as L

sig Vehicle {
	-- cruise control
	var cruise_control_lever :	one SCSLever,
	var cruise_control:		one CruiseControl,
	var desired_speed:		one Speed,
	-- sensors
	var key_state :				one KeyState,
	var engine :				one Engine,
	var rr_state :				one RRState,
	var rr_sensor :				one RRSensor,
	-- actuators
	var speed :				one Speed,
	var brake_pressure :		one BrakePressure,
	var acoustic_warning :	one AcousticWarning,
	var visual_warning :		one VisualWarning
}

enum SCSLever				{ Neutral, One, Two, Three, Four, Five, Six }
enum KeyState				{ NoKeyInserted, KeyInserted, KeyInIgnitionOnPosition }
enum Engine					{ On, Off }
enum RRState					{ Ready, Dirty, NotReady }
enum RRSensor				{ Clear, Obstacle, Unknown }
enum Speed					{ Stopped, Slow, Normal, Fast }
enum BrakePressure			{ Zero, Quarter, Half, Full }
enum AcousticWarning		{ AcousticOn, AcousticOff }
enum VisualWarning			{ VisualOn, VisualOff }
enum CruiseControl			{ CCOn, CCOff }

-- initialization
pred init [ v : Vehicle ] {
	v.cruise_control_lever = Neutral
	v.cruise_control = CCOff
	v.desired_speed = Stopped
	v.key_state = NoKeyInserted
	v.engine = Off
	v.rr_state = Ready
	v.rr_sensor = Clear
	v.speed = Stopped
	v.brake_pressure = Zero
	v.acoustic_warning = AcousticOff
	v.visual_warning = VisualOff
}

pred engine_and_keystate_dont_change [ v : Vehicle ] {
	v.engine' = v.engine
	v.key_state' = v.key_state
}

pred rr_and_warnings_dont_change [ v : Vehicle ] {
	v.rr_state' = v.rr_state
	v.rr_sensor' = v.rr_sensor
	v.acoustic_warning' = v.acoustic_warning
	v.visual_warning' = v.visual_warning
}

pred cruise_control_dont_change [ v : Vehicle ] {
	v.cruise_control_lever' = v.cruise_control_lever
	v.cruise_control' = v.cruise_control
	v.desired_speed' = v.desired_speed
}

pred speed_and_brake_dont_change [ v : Vehicle ] {
	v.speed' = v.speed
	v.brake_pressure' = v.brake_pressure
}

pred insert_key [ v : Vehicle ] {
	-- pre
	v.key_state = NoKeyInserted
	-- pos
	v.key_state' = KeyInserted
	-- frame
	v.engine' = v.engine
	rr_and_warnings_dont_change [ v ]
	cruise_control_dont_change [ v ]
	speed_and_brake_dont_change [ v ]
}

pred turn_engine_on [ v : Vehicle ] {
	-- pre
	v.key_state = KeyInserted
	v.engine = Off
	-- pos
	v.key_state' = KeyInIgnitionOnPosition
	v.engine' = On
	-- frame
	rr_and_warnings_dont_change [ v ]
	speed_and_brake_dont_change [ v ]
	cruise_control_dont_change [ v ]
}

pred speed_change_up [ v : Vehicle ] {
	-- pre
	v.engine = On
	v.cruise_control = CCOff
	-- pos
	v.speed' = v.speed.S/next
	v.engine' = On
	v.cruise_control' = CCOff
	-- frame
	v.key_state' = v.key_state
	v.brake_pressure' = v.brake_pressure
	v.cruise_control_lever' = v.cruise_control_lever
	v.desired_speed' = v.desired_speed
	rr_and_warnings_dont_change [ v ]
}

pred speed_change_down [ v : Vehicle ] {
	-- pre
	v.engine = On
	v.cruise_control = CCOff
	-- pos
	v.speed' = v.speed.S/prev
	v.engine' = On
	v.cruise_control' = CCOff
	-- frame
	v.key_state' = v.key_state
	v.brake_pressure' = v.brake_pressure
	v.cruise_control_lever' = v.cruise_control_lever
	v.desired_speed' = v.desired_speed
	rr_and_warnings_dont_change [ v ]
}

pred cruise_control_lever_default_neutral [ v : Vehicle ] {
	-- pre
	v.cruise_control_lever not in Neutral
	-- pos
	v.cruise_control_lever' = Neutral
	-- frame
	v.cruise_control' = v.cruise_control
	v.desired_speed' = v.desired_speed
	engine_and_keystate_dont_change [ v ]
	rr_and_warnings_dont_change [ v ]
	speed_and_brake_dont_change [ v ]
}

pred change_cruise_control_lever_one [ v : Vehicle ] {
	-- pre
	v.engine = On
	v.cruise_control = CCOff
	v.cruise_control_lever = Neutral
	not v.speed = Stopped
	-- pos
	v.engine' = On
	v.cruise_control' = CCOn
	v.cruise_control_lever' = One
	v.desired_speed' = v.speed
	not v.speed' = Stopped
	-- frame
	v.key_state' = v.key_state
	v.brake_pressure' = v.brake_pressure
	speed_and_brake_dont_change [ v ]
	rr_and_warnings_dont_change [ v ]
}

pred change_cruise_control_lever_two [ v : Vehicle ] {
	-- pre
	v.engine = On
	v.cruise_control = CCOn
	v.cruise_control_lever = Neutral
	-- pos
	v.engine' = On
	v.cruise_control' = CCOn
	v.cruise_control_lever' = Two
	v.desired_speed' = v.desired_speed.S/next
	v.speed' = v.desired_speed'
	-- frame
	v.key_state' = v.key_state
	v.brake_pressure' = v.brake_pressure
	rr_and_warnings_dont_change [ v ]
}

pred change_cruise_control_lever_three [ v : Vehicle ] {
	-- pre
	v.engine = On
	v.cruise_control = CCOn
	v.cruise_control_lever = Neutral
	-- pos
	v.engine' = On
	v.cruise_control' = CCOn
	v.cruise_control_lever' = Three
	v.desired_speed' = v.desired_speed.S/prev
	v.speed' = v.desired_speed'
	-- frame
	v.key_state' = v.key_state
	v.brake_pressure' = v.brake_pressure
	rr_and_warnings_dont_change [ v ]
}

pred change_cruise_control_lever_four [ v : Vehicle ] {
	-- pre
	v.engine = On
	v.cruise_control = CCOn
	v.cruise_control_lever = Neutral
	-- pos
	v.engine' = On
	v.cruise_control' = CCOff
	v.cruise_control_lever' = Four
	-- frame
	v.desired_speed' = v.desired_speed
	v.key_state' = v.key_state
	speed_and_brake_dont_change [ v ]
	rr_and_warnings_dont_change [ v ]
}

pred rr_sensor_changeTO_obstacle [ v : Vehicle ] {
	-- pre
	v.key_state = KeyInserted
	v.rr_state = Ready
	v.rr_sensor = Clear
	--pos
	v.key_state' = KeyInserted
	v.rr_state' = Ready
	v.rr_sensor' = Obstacle
	-- frame
	v.engine' = v.engine
	v.visual_warning' = v.visual_warning
	v.acoustic_warning' = v.acoustic_warning
	speed_and_brake_dont_change [ v ]
	cruise_control_dont_change [ v ]
}

pred rr_sensor_changeTO_clear [ v : Vehicle ] {
	-- pre
	v.rr_state = Ready
	v.rr_sensor = Obstacle
	--pos
	v.rr_state' = Ready
	v.rr_sensor' = Clear
	-- frame
	v.visual_warning' = v.visual_warning
	v.acoustic_warning' = v.acoustic_warning
	speed_and_brake_dont_change [ v ]
	cruise_control_dont_change [ v ]
	engine_and_keystate_dont_change [ v ]
}

pred rr_sensor_obstacle_visual_on [ v : Vehicle ] {
	-- pre
	v.rr_sensor = Obstacle
	v.speed in Stopped or v.speed in Slow
	v.visual_warning = VisualOff
	-- pos
	v.rr_sensor' = Obstacle
	v.speed' in Stopped or v.speed' in Slow
	v.visual_warning' = VisualOn
	-- frame
	v.rr_state' = v.rr_state
	v.acoustic_warning' = v.acoustic_warning
	v.brake_pressure' = v.brake_pressure
	cruise_control_dont_change [ v ]
	engine_and_keystate_dont_change [ v ]
}

pred rr_sensor_on_in_trasition_slow_normal [ v : Vehicle ] {
	-- pre
	v.engine = On
	v.rr_sensor = Obstacle
	v.speed = Slow
	v.visual_warning = VisualOn
	v.acoustic_warning = AcousticOff
	-- pos
	v.engine' = On
	v.rr_sensor' = v.rr_sensor
	v.speed' = Normal
	v.visual_warning' = VisualOff
	v.acoustic_warning' = AcousticOn
	-- frame
	v.brake_pressure' = v.brake_pressure
	v.rr_state' = v.rr_state
	v.key_state' = v.key_state
	cruise_control_dont_change [ v ]
}

pred rr_sensor_obstacle_visual_off [ v : Vehicle ] {
	-- pre
	v.rr_sensor = Clear
	v.visual_warning = VisualOn
	-- pos
	v.rr_sensor' = Clear
	v.visual_warning' = VisualOff
	-- frame
	v.speed' = v.speed
	v.rr_state' = v.rr_state
	v.acoustic_warning' = v.acoustic_warning
	v.brake_pressure' = v.brake_pressure
	cruise_control_dont_change [ v ]
	engine_and_keystate_dont_change [ v ]
}

pred rr_sensor_obstacle_acoustic_on [ v : Vehicle ] {
	-- pre
	v.rr_sensor = Obstacle
	v.speed in Normal or v.speed in Fast
	v.acoustic_warning = AcousticOff
	-- pos
	v.rr_sensor' = Obstacle
	v.speed' in Normal or v.speed' in Fast
	v.acoustic_warning' = AcousticOn
	-- frame
	v.rr_state' = v.rr_state
	v.visual_warning' = v.visual_warning
	v.brake_pressure' = v.brake_pressure
	cruise_control_dont_change [ v ]
	engine_and_keystate_dont_change [ v ]
}

pred rr_sensor_obstacle_acoustic_off [ v : Vehicle ] {
	-- pre
	v.rr_sensor = Clear
	v.speed = Normal or v.speed = Fast
	v.acoustic_warning = AcousticOn
	-- pos
	v.rr_sensor' = Clear
	v.speed' = Normal or v.speed' = Fast
	v.acoustic_warning' = AcousticOff
	-- frame
	v.rr_state' = v.rr_state
	v.visual_warning' = v.visual_warning
	v.brake_pressure' = v.brake_pressure
	cruise_control_dont_change [ v ]
	engine_and_keystate_dont_change [ v ]
}

-- do nothing
pred skip {
	cruise_control_lever' = cruise_control_lever
	cruise_control' = cruise_control
	key_state' = key_state
	engine' = engine
	rr_state' = rr_state
	rr_sensor' = rr_sensor
	speed' = speed
	brake_pressure' = brake_pressure
	acoustic_warning' = acoustic_warning
	visual_warning' = visual_warning
}

fact init {
	all v : Vehicle | init [v]
}

fact transitions {
	always ( all v : Vehicle | insert_key [ v ] or turn_engine_on [ v ] or
							speed_change_up [ v ] or
							speed_change_down [ v ] or
							change_cruise_control_lever_one [ v ] or
							change_cruise_control_lever_two [ v ] or
							change_cruise_control_lever_three [ v ] or
							change_cruise_control_lever_four [ v ] or
							cruise_control_lever_default_neutral [ v ] or
							rr_sensor_changeTO_obstacle [ v ] or
							rr_sensor_changeTO_clear [ v ] or
							rr_sensor_obstacle_visual_on [ v ] or
							rr_sensor_obstacle_visual_off [ v ]  or
							rr_sensor_obstacle_acoustic_on [ v ] or
							rr_sensor_obstacle_acoustic_off [ v ] or
							rr_sensor_on_in_trasition_slow_normal [ v ])
}

run {
//	eventually Vehicle.key_state = KeyInserted
	eventually Vehicle.engine = On
//	eventually Vehicle.cruise_control_lever = Three
	eventually Vehicle.visual_warning = VisualOn
} for 1 but exactly 1 Vehicle

/**
	TESTING PREDS
	eventually Vehicle.engine = On
	eventually Vehicle.speed = Slow
	eventually Vehicle.speed = Normal
	eventually Vehicle.cruise_control_lever = One
	eventually Vehicle.cruise_control = CCOn
	eventually Vehicle.cruise_control_lever = Neutral
	eventually Vehicle.cruise_control_lever = Four
	eventually Vehicle.cruise_control = CCOff
**/
